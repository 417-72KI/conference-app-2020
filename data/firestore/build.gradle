import dependencies.Dep

apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file('gradle/android.gradle')

kotlin {
    targets {
        fromPreset(presets.android, 'android')

        def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
        def iosPreset = (buildForDevice) ? presets.iosArm64 : presets.iosX64
        fromPreset(iosPreset, 'ios') {
            binaries {
                framework {
                    // Disable bitcode embedding for the simulator build.
                    if (!buildForDevice) {
                        embedBitcode("disable")
                    }
                }
            }
        }
    }
    sourceSets {
        commonMain.dependencies {
            api Dep.Kotlin.stdlibCommon
            api project(":model")
            api Dep.Kotlin.coroutinesCommon
        }
        androidMain {
            dependsOn commonMain
            dependencies {
                api Dep.Kotlin.stdlibJvm
                api Dep.Kotlin.coroutines
                api project(":corecomponent:androidcomponent")
                api project(':ext:log')

                implementation Dep.Kotlin.coroutinesPlayServices
                implementation Dep.Firebase.auth
                implementation Dep.Firebase.fireStore
                api Dep.AndroidX.lifecycleLiveData

                implementation Dep.Dagger.core
                implementation Dep.Dagger.androidSupport

            }
        }
        commonTest.dependencies {
            implementation Dep.Test.KotlinMultiPlatform.commonModuleTest
            implementation Dep.Test.KotlinMultiPlatform.commonModuleTestAnnotations
            implementation Dep.MockK.common
        }
        androidTest {
            dependsOn commonMain
            dependencies {
                implementation Dep.Test.KotlinMultiPlatform.jvmModuleTest
                implementation Dep.Test.KotlinMultiPlatform.jvmModuleTestJunit
                implementation Dep.MockK.jvm
            }
        }
    }
}

dependencies {
    kapt Dep.Dagger.compiler
    kapt Dep.Dagger.androidProcessor
}


configurations {
    compileClasspath
}
