import dependencies.Dep

apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.android.library'

apply from: rootProject.file('gradle/android.gradle')

kotlin {
    targets {
        fromPreset(presets.android, 'android')

        def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
        def iosPreset = (buildForDevice) ? presets.iosArm64 : presets.iosX64
        fromPreset(iosPreset, 'ios') {
            binaries {
                framework {
                    // Disable bitcode embedding for the simulator build.
                    if (!buildForDevice) {
                        embedBitcode("disable")
                    }
                }
            }
        }
    }
    sourceSets {
        commonMain.dependencies {
            api Dep.Kotlin.stdlibCommon
            api Dep.Timber.common
        }
        androidMain {
            dependencies {
                api Dep.Kotlin.stdlibJvm
                api Dep.Timber.jdk
            }
        }
        commonTest.dependencies {
            implementation Dep.Test.KotlinMultiPlatform.commonModuleTest
            implementation Dep.Test.KotlinMultiPlatform.commonModuleTestAnnotations
            implementation Dep.MockK.common
        }
        androidTest {
            dependencies {
                implementation Dep.Test.KotlinMultiPlatform.jvmModuleTest
                implementation Dep.Test.KotlinMultiPlatform.jvmModuleTestJunit
                implementation Dep.MockK.jvm
            }
        }
    }
}


configurations {
    compileClasspath
}
